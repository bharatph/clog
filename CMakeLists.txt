cmake_minimum_required(VERSION 3.5.1)
project(clog VERSION 0.5.0)

set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${PROJECT_SOURCE_DIR}/cmake")

# Set the build type if not set
if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE "Release")
endif()

add_library(clog ${CMAKE_CURRENT_SOURCE_DIR}/src/clog.c)
target_include_directories(clog PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:include>)

if(CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
    set(WARNINGS "-Werror" "-Wall")
elseif(CMAKE_CXX_COMPILER_ID MATCHES "Clang")
    set(WARNINGS "-Werror" "-Wall")
elseif(MSVC)
    set(WARNINGS "/WX" "/W4")
endif()
target_compile_options(clog PRIVATE ${WARNINGS})

if (${CMAKE_SOURCE_DIR} STREQUAL ${CMAKE_CURRENT_SOURCE_DIR})
	include(GNUInstallDirs)

	set(CLOG_INC_INSTALL_DIR "${CMAKE_INSTALL_INCLUDEDIR}")
	set(CLOG_RUNTIME_INSTALL_DIR "${CMAKE_INSTALL_BINDIR}")
	set(CLOG_LIBRARY_INSTALL_DIR "${CMAKE_INSTALL_LIBDIR}")
	set(CLOG_ARCHIVE_INSTALL_DIR "${CMAKE_INSTALL_LIBDIR}")
	set(CLOG_FRAMEWORK_INSTALL_DIR "${CMAKE_INSTALL_LIBDIR}")

	set(CLOG_CMAKE_CONFIG_INSTALL_DIR "${CMAKE_INSTALL_DATADIR}/clog/cmake")
	set(CLOG_ADDITIONAL_FILES_INSTALL_DIR "${CMAKE_INSTALL_DATADIR}/clog")

	# Install target
	install(TARGETS clog EXPORT clogTargets
		RUNTIME       DESTINATION ${CLOG_RUNTIME_INSTALL_DIR}
		LIBRARY       DESTINATION ${CLOG_LIBRARY_INSTALL_DIR}
		ARCHIVE       DESTINATION ${CLOG_ARCHIVE_INSTALL_DIR}
		FRAMEWORK     DESTINATION ${CLOG_FRAMEWORK_INSTALL_DIR})

	# Install headers
	install(DIRECTORY "${CMAKE_CURRENT_SOURCE_DIR}/include/"
		DESTINATION ${CLOG_INC_INSTALL_DIR})

	# Make cmake config files for all targets
	install(EXPORT  clogTargets
		DESTINATION ${CLOG_CMAKE_CONFIG_INSTALL_DIR}
		FILE clog-config.cmake)

	# Install readme and license
	install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE" "${CMAKE_CURRENT_SOURCE_DIR}/README.md"
		DESTINATION ${CLOG_ADDITIONAL_FILES_INSTALL_DIR})
endif()

if (CLOG_TESTS)
	add_subdirectory(test)
endif()
